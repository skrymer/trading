plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.1.21'
	id 'org.jetbrains.kotlin.plugin.spring' version '2.1.21'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.jlleitschuh.gradle.ktlint' version '12.1.2'
	id 'nu.studer.jooq' version '9.0'
}

group = 'com.skrymer'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url "https://jitpack.io" }
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}

ext {
	set('springAiVersion', '1.0.3')
}

dependencies {
	// Database - H2 with jOOQ
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.jooq:jooq-kotlin' // Version managed by Spring Boot
	implementation 'com.h2database:h2:2.2.224'

	// Flyway for database migrations
	implementation 'org.flywaydb:flyway-core'

	// jOOQ code generation dependencies
	jooqGenerator 'com.h2database:h2:2.2.224'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
	implementation 'org.jetbrains.kotlin:kotlin-reflect'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0'
	implementation "de.siegmar:fastcsv:4.0.0"
	implementation "org.apache.httpcomponents.client5:httpclient5-win:5.2.3"
	implementation "com.github.polygon-io:client-jvm:v5.1.2"
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.2'

	// Spring AI MCP dependencies
	implementation 'org.springframework.ai:spring-ai-starter-mcp-server-webmvc'

	// Caffeine cache for better performance
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
	testImplementation 'org.mockito.kotlin:mockito-kotlin:5.1.0'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
	}
	dependencies {
		// Force jOOQ version consistency
		dependency 'org.jooq:jooq:3.19.23'
		dependency 'org.jooq:jooq-kotlin:3.19.23'
		dependency 'org.jooq:jooq-meta:3.19.23'
		dependency 'org.jooq:jooq-codegen:3.19.23'
	}
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll '-Xjsr305=strict'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

ktlint {
	version = "1.5.0"
	verbose = true
	android = false
	outputToConsole = true
	ignoreFailures = false
	enableExperimentalRules = false
	filter {
		exclude("**/generated/**")
		include("**/kotlin/**")
	}
}

jooq {
	version = '3.19.23' // Match Spring Boot's managed version
	configurations {
		main {
			generateSchemaSourceOnCompilation = true
			generationTool {
				jdbc {
					driver = 'org.h2.Driver'
					url = 'jdbc:h2:file:~/.trading-app/database/trading'
					user = 'sa'
					password = ''
				}
				generator {
					name = 'org.jooq.codegen.KotlinGenerator'
					database {
						name = 'org.jooq.meta.h2.H2Database'
						includes = '.*'
						excludes = ''
						inputSchema = 'PUBLIC'
					}
					generate {
						deprecated = false
						records = true
						pojos = true
						daos = false
						kotlinNotNullPojoAttributes = true
						kotlinNotNullRecordAttributes = true
					}
					target {
						packageName = 'com.skrymer.udgaard.jooq'
						directory = 'build/generated-src/jooq/main'
					}
				}
			}
		}
	}
}
